// These imports are needed further down
// -------------------------------------
import org.jooq.util.GenerationTool

import javax.xml.bind.JAXB

apply plugin: 'java'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = 'sonatype-snapshots'
            url = 'https://oss.sonatype.org/content/repositories/snapshots'
        }
    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.0-SNAPSHOT'
        classpath 'org.jooq:jooq-codegen:3.6.0'
        classpath group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '1.2.+'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

minecraft {
    version = "11.14.3.1550"
    runDir = "run"
    mappings = "snapshot_20151031"
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        name 'Sponge maven repo'
        url 'http://repo.spongepowered.org/maven'
    }
    maven {
        name 'sk89q-repo'
        url 'http://maven.sk89q.com/repo/'
    }
}

dependencies {
    compile group: 'org.jooq', name: 'jooq', version: '3.7.0'
    compile group: 'com.sk89q.worldedit', name: 'worldedit-forge-mc1.8', version: '6.1.1-SNAPSHOT'
    compile group: 'org.spongepowered', name: 'spongeapi', version: '2.+'

    runtime group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '1.2.3'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
version = '1.0'

sourceSets {
    main {
        resources.srcDirs = ["assets/Primary"]
    }
}

task codegen << {
    println 'Generating new schema classes...'
    def writer = new StringWriter()
    def xml = new groovy.xml.MarkupBuilder(writer)
            .configuration('xmlns': 'http://www.jooq.org/xsd/jooq-codegen-3.6.0.xsd') {
        jdbc() {
            driver('org.mariadb.jdbc.Driver')
            url('jdbc:mariadb://localhost:3306')
            user('mc_db_dev')
            password('mc_db_dev')
        }
        generator() {
            database() {
                name('org.jooq.util.mariadb.MariaDBDatabase')
                inputSchema('mc_db')
            }
            generate() {
            }
            target() {
                packageName('com.skelril.skree.db.schema')
                directory('src/main/java')
            }
        }
    }

    GenerationTool.main(
            JAXB.unmarshal(new StringReader(writer.toString()), org.jooq.util.jaxb.Configuration.class)
    )
}
